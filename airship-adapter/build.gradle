plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.gimbal.android'
description = "Use Gimbal Place triggers to track location-based Airship CustomEvents"
version = rootProject.adapterVersion

android {
    namespace = 'com.gimbal.android'

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion as Integer
        targetSdkVersion rootProject.targetSdkVersion as Integer
        aarMetadata {
            minCompileSdk = 28
        }
    }

    buildTypes {
        release {}
        debug {}
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api "com.gimbal.android.v4:gimbal-sdk:${rootProject.gimbalVersion}"
    implementation "com.gimbal.android.v4:gimbal-slf4j-impl:${rootProject.gimbalVersion}"
    compileOnly "com.urbanairship.android:urbanairship-core:${rootProject.airshipVersion}"

    implementation "androidx.startup:startup-runtime:1.1.1"
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }

            pom {
                name = "Gimbal Airship Adapter"
                description = project.description
                url = "https://docs.gimbal.com"

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'Gimbal Developers'
                        email = 'developers@gimbal.com'
                        organization = 'Infillion'
                        organizationUrl = 'https://infillion.com'
                    }
                }

                contributors {
                    contributor {
                        name = 'Urban Airship'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/PaeDae/android-airship-adapter.git'
                    developerConnection = 'scm:git:git@github.com/PaeDae/android-airship-adapter.git'
                    url = 'https://github.com/PaeDae/android-airship-adapter'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.release
}

